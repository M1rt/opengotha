/*
 * This file is part of OpenGotha.
 *
 * OpenGotha is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 2 of the License, or
 * (at your option) any later version.
 *
 * OpenGotha is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with OpenGotha.  If not, see <http://www.gnu.org/licenses/>.
 */

import org.apache.tools.ant.filters.EscapeUnicode

version '3.46.04-RGF.1-SNAPSHOT'

repositories {
    mavenCentral()
}

apply plugin: 'java'
apply plugin: 'application'

compileJava {
    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'
}

mainClassName = 'info.vannier.gotha.JFrGothaStart'

tasks.withType(ProcessResources).each { task ->
    task.from(task.getSource()) {
        include '**/*.properties'
        filter(EscapeUnicode)
    }
}

startScripts {
    doLast {
        unixScript.text = unixScript.text.replace('cd "`dirname \\"$PRG\\"`/../.." >/dev/null', 'cd "`dirname \\"$PRG\\"`" >/dev/null')
        windowsScript.text =  windowsScript.text.replace('set APP_HOME=%DIRNAME%..\\..', 'set APP_HOME=%DIRNAME%')
        // See https://github.com/gradle/gradle/issues/4675
    }
}

application {
    executableDir = "/$applicationName-$version"
}

distTar {
    compression = Compression.GZIP
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}

dependencies {
    compile group: 'com.google.code.gson', name: 'gson', version: '2.8.5'
    compile group: 'com.sun.woodstock.dependlibs', name: 'jhall', version: '2.0'
    compile group: 'com.miglayout', name: 'miglayout-swing', version: '5.2'
    compile group: 'com.ibm.icu', name: 'icu4j', version: '60.2'
    testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.3.2'
    testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: '5.3.2'
    testCompile group: 'nl.jqno.equalsverifier', name: 'equalsverifier', version: '3.0.3'
    testRuntime group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.3.2'
}
