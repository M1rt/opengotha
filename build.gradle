/*
 * This file is part of OpenGotha.
 *
 * OpenGotha is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 2 of the License, or
 * (at your option) any later version.
 *
 * OpenGotha is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with OpenGotha.  If not, see <http://www.gnu.org/licenses/>.
 */

import org.apache.tools.ant.filters.EscapeUnicode

version '3.46.04-RGF.1'

repositories {
    jcenter()
}

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'application'

buildscript {
    ext.kotlin_version = '1.3.50'
    ext.coroutines_version = '1.3.2'
    ext.serialization_version = '0.13.0'
    ext.ktor_version = '1.2.5'
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-serialization:$kotlin_version"
    }
}

compileJava {
    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'
    options.encoding = 'UTF-8'
}

mainClassName = 'info.vannier.gotha.JFrGothaStart'

tasks.withType(ProcessResources).each { task ->
    task.from(task.getSource()) {
        include '**/*.properties'
        filter(EscapeUnicode)
    }
}

task createProperties {
    def version = project.version
    def outFile = file("${buildDir}/resources/main/build.properties")
    inputs.property 'version', version
    outputs.file outFile
    doLast {
        file("${buildDir}/resources/main").mkdirs()
        file(outFile).withWriter { w ->
            Properties p = new Properties()
            p['version'] = project.version.toString()
            p.store w, null
        }
    }
}
processResources.dependsOn createProperties

startScripts {
    doLast {
        unixScript.text = unixScript.text.replace('cd "`dirname \\"$PRG\\"`/../.." >/dev/null', 'cd "`dirname \\"$PRG\\"`" >/dev/null')
        windowsScript.text =  windowsScript.text.replace('set APP_HOME=%DIRNAME%..\\..', 'set APP_HOME=%DIRNAME%')
        // See https://github.com/gradle/gradle/issues/4675
    }
}

application {
    executableDir = "/$applicationName-$version"
}

distTar {
    compression = Compression.GZIP
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}

dependencies {
    implementation project(':rgf')
    implementation group: 'com.ibm.icu', name: 'icu4j', version: '60.2'
    implementation group: 'com.miglayout', name: 'miglayout-swing', version: '5.2'
    implementation group: 'com.sun.woodstock.dependlibs', name: 'jhall', version: '2.0'
    implementation group: 'com.toedter', name: 'jcalendar', version: '1.4'
    implementation group: 'org.apache.poi', name: 'poi-ooxml', version: '3.17'
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-swing:$coroutines_version"
    testImplementation "org.jetbrains.kotlin:kotlin-test:$kotlin_version"
    testImplementation "org.jetbrains.kotlin:kotlin-test-junit5:$kotlin_version"
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.3.2'
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: '5.3.2'
    testImplementation group: 'nl.jqno.equalsverifier', name: 'equalsverifier', version: '3.0.3'
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.3.2'
}
